buildscript {
    ext.kotlin_version = '1.1.+'
    ext.spek_version = "1.0.+"
    ext.mockito_version = "1.5.+"
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

group 'org.amshove.kluent'
version '1.30'

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def isAndroid = project.rootProject.hasProperty("ANDROID")
if(isAndroid) {
    logger.warn("Android build!")
}
archivesBaseName = isAndroid ? "kluent-android" : "kluent"

repositories {
    jcenter()
    mavenCentral()
    maven {
        url = 'http://repository.jetbrains.com/all'
    }
}

dependencies {
    implementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.nhaarman:mockito-kotlin-kt1.1:$mockito_version"
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.spek:spek:$spek_version"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockito_version"
}

sourceSets {
    main {
        kotlin {
            srcDirs += 'src/main/kotlin'

            if(isAndroid) {
                exclude '**/*Backtick.kt'
            }
        }
    }
    test {
        kotlin {
            srcDirs += 'src/test/kotlin'

            if(isAndroid) {
                exclude '**/backtick*/**'
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

bintrayUpload.dependsOn install

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['PublishKluent']
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = isAndroid ? "Kluent-Android" : "Kluent"
        licenses = ['MIT']
        vcsUrl = 'https://github.com/MarkusAmshove/Kluent'
    }
}

publishing {
    publications {
        PublishKluent(MavenPublication) {
            from components.java
            groupId 'org.amshove.kluent'
            artifactId isAndroid ? 'kluent-android' : 'kluent'
            version project.version

            // http://stackoverflow.com/a/32353697/6556970
            pom.withXml {
                Map resolvedVersionMap = [:]
                Set<ResolvedArtifact> resolvedArtifacts = configurations.compile.getResolvedConfiguration().getResolvedArtifacts()
                resolvedArtifacts.addAll(configurations.testCompile.getResolvedConfiguration().getResolvedArtifacts())
                resolvedArtifacts.each {
                    ModuleVersionIdentifier mvi = it.getModuleVersion().getId();
                    resolvedVersionMap.put("${mvi.getGroup()}:${mvi.getName()}", mvi.getVersion())
                }

                asNode().dependencies.first().each {
                    def groupId = it.get("groupId").first().value().first()
                    def artifactId = it.get("artifactId").first().value().first()
                    it.get("version").first().value = resolvedVersionMap.get("${groupId}:${artifactId}")
                }
            }
        }
    }
}
